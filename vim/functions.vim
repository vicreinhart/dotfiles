" functions.vim {{{
"
" This file holds non-oneline functions that do not neatly fit into
" my key maps file.
" -------------------------------------------------------------------------- }}}
" Line Return {{{
"
" Make sure Vim returns to the same line when you reopen a file.
augroup line_return
    au!
    au BufReadPost *
        \ if line("'\"") > 0 && line("'\"") <= line("$") |
        \     execute 'normal! g`"zvzz' |
        \ endif
augroup END
" -------------------------------------------------------------------------- }}}
" Jumping to tags. {{{
"
" Basically, <c-]> jumps to tags and <c-\> opens the tag in a new
" split instead.
"
" Both of them will align the destination line to the upper middle part of the
" screen.  Both will pulse the cursor line so you can see where are.  <c-\> 
" will also fold everything in the buffer and then unfold just enough for you
" to see the destination line.
function! JumpToTag()
    execute "normal! \<c-]>mzzvzz15\<c-e>"
    execute "keepjumps normal! `z"
    Pulse
endfunction

function! JumpToTagInSplit()
    execute "normal! \<c-w>v\<c-]>mzzMzvzz15\<c-e>"
    execute "keepjumps normal! `z"
    Pulse
endfunction
nnoremap <c-]> :silent! call JumpToTag()<cr>
nnoremap <c-\> :silent! call JumpToTagInSplit()<cr>
" -------------------------------------------------------------------------- }}}
" Visual Mode */# from Scrooloose {{{
function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction
vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>
" -------------------------------------------------------------------------- }}}
" Temporary file location and file handling {{{ 
set autoread
set autowrite
set backup
set backupdir=~/.vim/tmp/backup                  " Backups.
set directory=~/.vim/tmp/backup                  " Swap files 
set noswapfile
set undodir=~/.vim/tmp/backup                    " Swap files 
set undofile
set undoreload=10000

" Make those folders automatically if they don't already exist.
if !isdirectory(expand(&undodir))
   call mkdir(expand(&undodir), "p")
endif
if !isdirectory(expand(&backupdir))
   call mkdir(expand(&backupdir), "p")
endif
if !isdirectory(expand(&directory))
   call mkdir(expand(&directory), "p")
endif
" -------------------------------------------------------------------------- }}}
